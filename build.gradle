buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal() 
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.7' 
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group = 'pl.norbit'
version = 'v1.5.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    implementation 'fr.skytasul:glowingentities:1.4.3'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.github.LeonMangler:SuperVanish:6.2.18-3'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

shadowJar {
    archiveClassifier = ''
    minimize() 
    relocate 'fr.skytasul', 'pl.norbit.lib.glowingentities'
    mergeServiceFiles()
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

def targetJavaVersion = 21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
